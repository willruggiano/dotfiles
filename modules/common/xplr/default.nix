{
  config,
  lib,
  pkgs,
  ...
}:
with lib; let
  cfg = config.programs.xplr;
in {
  options.programs.xplr = {
    enable = mkEnableOption "xplr";
  };

  config = mkIf cfg.enable {
    user.packages = with pkgs; [xplr];

    home.configFile = let
      fzf-xplr = pkgs.stdenv.mkDerivation {
        name = "fzf-xplr";
        src = pkgs.fetchFromGitHub {
          owner = "sayanarijit";
          repo = "fzf.xplr";
          rev = "5373cee9d2716f774d593af67812f8b4c0ccced8";
          hash = "sha256-MQALJ6xn+c3oIuFlrzcCzdoSTwqgZKwQ/kUyJ7lQLLA=";
        };

        dontConfigure = true;
        dontBuild = true;
        dontFixup = true;

        installPhase = ''
          mkdir -p $out/lua/fzf
          cp init.lua $out/lua/fzf/init.lua
        '';
      };

      runtimeEnv = pkgs.buildEnv {
        name = "xplr-runtime-env";
        paths = [fzf-xplr];
      };
    in {
      "xplr/init.lua".text = ''
        -- Generated by Nix
        version = "${pkgs.xplr.version}"

        package.path = "${runtimeEnv}/lua/?/init.lua;;"

        require("fzf").setup{
          mode = "default",
          key = "F",
          args = os.getenv("FZF_DEFAULT_OPTS")
        }
      '';
    };
  };
}
